apply plugin: 'checkstyle'
apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.1'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.github.tomakehurst:wiremock:1.47'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
        testLogging.showStandardStreams = true
    }

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

test {
    useJUnit {
        excludeCategories 'com.box.sdk.IntegrationTest'
    }
}

task integrationTest(type: Test) {
    description 'Runs the integration tests.'
    group 'Verification'

    useJUnit {
        includeCategories 'com.box.sdk.IntegrationTest'
    }
}
